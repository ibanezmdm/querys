USE [RepNonFood]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author:			<Sebastian Cornejo>
-- Create date: <2019/10/17>
-- Update date: <2019/10/18>
-- Description:	<Carga datos base para informe Non Food ASR desde archivo excel>
-- ?? EXEC [RepNonFood].[dbo].[SP_NF_BASE_ASR__CARGA_INFORME]
-- =============================================
CREATE PROCEDURE [dbo].[SP_NF_BASE_ASR__CARGA_INFORME]
AS
BEGIN

	BEGIN TRY

		TRUNCATE TABLE [RepNonFood].[dbo].[NF_BASE_ASR_INFORME]

		INSERT INTO [RepNonFood].[dbo].[NF_BASE_ASR_INFORME]

		SELECT
			E.*,
			M.NOM_SKU,
			M.DIVISION,
			M.DEPARTAMENTO,
			M.SUBDEPARTAMENTO,
			M.CLASE,
			M.SUBCLASE,
			M.ESTADO,
			ML.NOM_LOCAL,
			M.PROCEDENCIA,
			SCD.UN_INV_DISP_HOY,
			C.CASEPACK, 
			D.ON_ORDER_ASR,
			D.ON_HAND_ASR,
			D.DEMANDA_SEMANAL_ASR,
			DM.METOD_ABAST,
			DM.TBL_CODE_SYST SIST_REPO,
			CASE
				WHEN D.ON_HAND_ASR IS NULL THEN 'SUBIR_ASR'
				ELSE 'OK'
			END SUBIR_A_ASR,
			CASE
				WHEN ((ISNULL(C.CASEPACK,0)*0.8 >= ISNULL(E.MINIMO_BASE,0)) AND (ISNULL(C.CASEPACK,0)*0.8 >= ISNULL(D.DEMANDA_SEMANAL_ASR,0)) AND (ISNULL(C.CASEPACK,0)*0.8 >= 3)) 
					THEN ISNULL(C.CASEPACK,0)*0.8
				WHEN ((ISNULL(E.MINIMO_BASE,0) >= ISNULL(C.CASEPACK,0)*0.8) AND (ISNULL(E.MINIMO_BASE,0) >= ISNULL(D.DEMANDA_SEMANAL_ASR,0)) AND (ISNULL(E.MINIMO_BASE,0) >= 3)) 
					THEN ISNULL(E.MINIMO_BASE,0)
				WHEN ((ISNULL(D.DEMANDA_SEMANAL_ASR,0) >= ISNULL(C.CASEPACK,0)*0.8) AND (ISNULL(D.DEMANDA_SEMANAL_ASR,0) >= ISNULL(E.MINIMO_BASE,0)) AND (ISNULL(D.DEMANDA_SEMANAL_ASR,0) >= 3)) 
					THEN D.DEMANDA_SEMANAL_ASR
				ELSE 3
			END OBJETIVO,
			CASE
				WHEN ISNULL(D.ON_ORDER_ASR,0) > 0 THEN 1
				ELSE 0
			END INDICADOR,
			CASE
				WHEN ISNULL(D.ON_ORDER_ASR,0) > 0 THEN 0
				ELSE 1
			END NO_EMPUJADO
		FROM [RepNonFood].[dbo].[NF_BASE_ASR_EXCEL] E
		LEFT JOIN [INFORMES3].[dbo].[N_OTB_2_DATOS_ASR] D
			ON E.SKU = D.SKU
				AND E.COD_LOCAL = D.COD_LOCAL
		LEFT JOIN (
			SELECT
				[SKU]
				, SUM([COSTO_INV_DISP_HOY]) [COSTO_INV_DISP_HOY]
				, SUM([MONTO_INV_DISP_HOY]) [MONTO_INV_DISP_HOY]
				, SUM([UN_INV_DISP_HOY]) [UN_INV_DISP_HOY]
			FROM [INSTOCK_OPT].[dbo].[STOCK_HOY_CIA]
			WHERE [DIVISION] IN ('J08- VESTUARIO', 'J09 - HOGAR', 'J10 - BAZAR', 'J11 - ELECTRO')
			GROUP BY [SKU]
		) SCD
			ON E.SKU = SCD.SKU
		LEFT JOIN [RepNonFood].[dbo].[MAESTRA_SKU] M
			ON E.SKU = M.SKU
		LEFT JOIN [INFORMES3].dbo.MAESTRA_CASEPACK c
			ON E.SKU = c.SKU
		LEFT JOIN [192.168.148.177].[DATOS_MAESTROS].[dbo].[SURTIDO_LOCAL] DM
			ON E.SKU = DM.PRD_LVL_NUMBER
				AND E.COD_LOCAL = ORG_LVL_NUMBER
		LEFT JOIN [INFORMES3].[DBO].[MAESTRA_LOCALES_OFICIAL] ML
			ON E.COD_LOCAL = ML.COD_LOCAL

	END TRY

	BEGIN CATCH

		EXEC [ACTUALIZABLES].dbo.SP_GetErrorInfo 201, 'Job', 'NF_BASE_ASR__CARGA_BASE_INFORME', '[RepNonFood].[dbo].[NF_BASE_ASR_INFORME]'

	END CATCH

END
