USE [INFORMES3]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author:		<Author,,Name>
-- Create date: <Create Date,,>
-- Updated_at: <2020/01/10>
-- Detalles:
-- 	-> 2020/01/10: Consulta se genera a la tabla MAIL_INSTOCK_SUBDEP_2
-- EXEC [INFORMES3].[dbo].[Sp_NUEVO_INSTOCK_MAIL_SUBDEPTO]
-- =============================================
ALTER PROCEDURE [dbo].[Sp_NUEVO_INSTOCK_MAIL_SUBDEPTO]
AS
BEGIN

	DECLARE @temp_semana AS TABLE (
		N_SEMANA INT,
		SEMANA INT
	)

	INSERT INTO @temp_semana

	SELECT
		ROW_NUMBER() OVER(ORDER BY SEMANA DESC) - 1 N_SEMANA,
		SEMANA
	FROM (
		SELECT DISTINCT TOP 5 F.SEMANA
		FROM [INFORMES3].[dbo].[MAIL_INSTOCK_SUBDEP_2] I
		LEFT JOIN [INSTOCK_OPT].dbo.DHW_MES_GC F
			ON F.FECHA = I.FECHA_ACTUALIZ
		ORDER BY SEMANA DESC
	) TempSem


	TRUNCATE TABLE [INFORMES3].[dbo].[NUEVO_INSTOCK_MAIL_SUBDEPTO]

	INSERT INTO [INFORMES3].[dbo].[NUEVO_INSTOCK_MAIL_SUBDEPTO] (
		[SUBDEP]
		,[SEM4]
		,[SEM3]
		,[SEM2]
		,[SEM1]
		,[SEM_ACTUAL]
	)


	SELECT
		SUBDEP,
		SEM4,
		SEM3,
		SEM2,
		SEM1,
		SEM_ACTUAL
	FROM (

		/* Consulta instock semana Actual
		*/
		SELECT
			CASE 
				WHEN TS.N_SEMANA = 0 THEN 'SEM_ACTUAL'
				ELSE 'SEM' + CONVERT(VARCHAR(1), TS.N_SEMANA)
			END SEMANA,
			SUBDEP,
			CONVERT(NUMERIC(12,2) , ISNULL(SUM([NUM_OH_VALORIZADO]) / SUM([NUM_VTA_SEM_X_PERFIL]) * 100, 0)) AS INSTOCK
		FROM [INFORMES3].[dbo].[MAIL_INSTOCK_SUBDEP_2] I
		LEFT JOIN [INSTOCK_OPT].dbo.DHW_MES_GC F
			ON F.FECHA = I.FECHA_ACTUALIZ
		INNER JOIN @temp_semana TS
			ON TS.SEMANA = F.SEMANA
		WHERE
			(
				(DIVISION in ('J05 - FLC', 'J01 - PGC COMESTIBLE','J02 - PGC NO COMESTIBLE') AND SIS_REPOSICION IN ('Reposicion x ASR', 'Informar a ASR'))
				OR (DIVISION in ('J06 - PANADERIA Y PASTELERIA','J07 - PLATOS PREPARADOS') AND SIS_REPOSICION IN ('Reposicion x ASR'))
			)
		GROUP BY
			F.SEMANA,
			TS.N_SEMANA,
			SUBDEP

	) AS I
	PIVOT (
		SUM(I.INSTOCK)
		FOR SEMANA IN (
			SEM4,
			SEM3,
			SEM2,
			SEM1,
			SEM_ACTUAL
		)
	) AS pvtable

END 