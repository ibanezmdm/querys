USE [INFORMES3]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author: Sebastian E Cornejo B
-- Created_at: 2019/11/21
-- Updated_at: 2019/01/09
-- Description: EXEC SP_NUEVO_INSTOCK_MAIL_CUADRO_RESUMEN_v2
-- Detalle: 
-- 	-> 2019/01/09: Consulta se genera a partir de la tabla MAIL_INSTOCK_SUBDEP_2_TOP_DIV
-- =============================================

ALTER PROCEDURE [dbo].[SP_NUEVO_INSTOCK_MAIL_CUADRO_RESUMEN_v2]

AS
BEGIN

	DECLARE @temp_semana AS TABLE (
		N_SEMANA INT,
		SEMANA INT
	)

	INSERT INTO @temp_semana
	SELECT
		ROW_NUMBER() OVER(ORDER BY SEMANA DESC) - 1 N_SEMANA,
		SEMANA
	FROM (
		SELECT DISTINCT TOP 5 SEMANA
		FROM INSTOCK_OPT.dbo.MAIL_INSTOCK_SUBDEP_2_TOP_DIV I
		LEFT JOIN INSTOCK_OPT.dbo.DHW_MES_GC AS F
			ON F.FECHA = CONVERT(DATE, FECHA_ACTUALIZ)
		ORDER BY SEMANA DESC
	) TempSem



	TRUNCATE TABLE [INFORMES3].[dbo].[NUEVO_INSTOCK_MAIL_CUADRO_RESUMEN]


	INSERT INTO [INFORMES3].[dbo].[NUEVO_INSTOCK_MAIL_CUADRO_RESUMEN] (
		[CUADRO_RESUMEN]
		,[SEM4]
		,[SEM3]
		,[SEM2]
		,[SEM1]
		,[SEM_ACTUAL]
	)

	SELECT 
		ISNULL(CUADRO_RESUMEN, 0) CUADRO_RESUMEN,
		ISNULL(SEM4, 0) SEM4,
		ISNULL(SEM3, 0) SEM3,
		ISNULL(SEM2, 0) SEM2,
		ISNULL(SEM1, 0) SEM1,
		ISNULL(SEM_ACTUAL, 0) SEM_ACTUAL
	FROM (
		SELECT
			CUADRO_RESUMEN,
			CONVERT(NUMERIC(12, 2), SUM(NUM_OH_VALORIZADO) / NULLIF (SUM(NUM_VTA_SEM_X_PERFIL), 0) * 100) AS INSTOCK_G,
			CASE 
				WHEN S.N_SEMANA = 0 THEN 'SEM_ACTUAL'
				ELSE 'SEM' + CONVERT(VARCHAR(1), S.N_SEMANA)
			END SEMANA
		FROM (

			SELECT 
				SEMANA,
				NUM_OH_VALORIZADO,
				NUM_VTA_SEM_X_PERFIL,
				CUADRO_RESUMEN,
				FILTRO
			FROM (
				SELECT 
					F.SEMANA SEMANA,
					SUM(NUM_OH_VALORIZADO) NUM_OH_VALORIZADO,
					SUM(NUM_VTA_SEM_X_PERFIL) NUM_VTA_SEM_X_PERFIL,
					1 [COMPAÑIA],
					CASE WHEN TOP_500 = 'TOP500' THEN 1 END TOP500,
					CASE WHEN TOP_2100 = 'TOP2100' THEN 1 END TOP2100,
					CASE WHEN EDLP = 'X' THEN 1 END EDLP,
					CASE WHEN MMPP = 'MMPP' THEN 1 END MMPP,
					
					-- !! FILTRO PGC
					CASE
						WHEN ((DIVISION IN ('J01 - PGC COMESTIBLE','J02 - PGC NO COMESTIBLE')	AND SIS_REPOSICION IN ('Reposicion x ASR', 'Informar a ASR'))) 
						THEN 1
					END PGC,

					-- !! FILTRO PERECIBLES
					CASE
						WHEN ((DIVISION in ('J05 - FLC') AND SIS_REPOSICION in ('Reposicion x ASR', 'Informar a ASR'))
							OR (DIVISION in ('J06 - PANADERIA Y PASTELERIA','J07 - PLATOS PREPARADOS') AND SIS_REPOSICION in ('Reposicion x ASR'))) 
						THEN 1
					END PERECIBLES

				FROM INSTOCK_OPT.dbo.MAIL_INSTOCK_SUBDEP_2_TOP_DIV I
				LEFT JOIN INSTOCK_OPT.dbo.DHW_MES_GC AS F
					ON F.FECHA = CONVERT(DATE, FECHA_ACTUALIZ)
				WHERE
					(
						(DIVISION IN ('J01 - PGC COMESTIBLE', 'J02 - PGC NO COMESTIBLE', 'J05 - FLC') AND SIS_REPOSICION IN ('Reposicion x ASR','Informar a ASR'))
						OR (DIVISION IN ('J06 - PANADERIA Y PASTELERIA','J07 - PLATOS PREPARADOS')AND SIS_REPOSICION in ('Reposicion x ASR'))
					)
				GROUP BY 
					SEMANA,
					TOP_2100,
					TOP_500,
					EDLP,
					MMPP,
					SIS_REPOSICION,
					DIVISION
			) I
			UNPIVOT (
				FILTRO FOR CUADRO_RESUMEN IN (
					[COMPAÑIA],
					TOP500,
					TOP2100,
					EDLP,
					MMPP,
					PGC,
					PERECIBLES
				)
			) AS unpvt
		) AS I
		INNER JOIN @temp_semana S
			ON S.SEMANA = I.SEMANA
		GROUP BY 
			CUADRO_RESUMEN, 
			S.N_SEMANA
	) I
	PIVOT (
		SUM(INSTOCK_G) FOR SEMANA IN (
			SEM4,
			SEM3,
			SEM2,
			SEM1,
			SEM_ACTUAL
		)
	) pvtbl

END