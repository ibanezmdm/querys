USE [INFORMES3]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author: Sebastian E Cornejo B
-- Created_at: 2019/11/21
-- Updated_at: 2019/11/22
-- Description: EXEC SP_NUEVO_INSTOCK_MAIL_CUADRO_RESUMEN_v2
-- =============================================

ALTER PROCEDURE [dbo].[SP_NUEVO_INSTOCK_MAIL_CUADRO_RESUMEN_v2]

AS
BEGIN

DECLARE @temp_semana AS TABLE (
	N_SEMANA INT,
	SEMANA INT
)

INSERT INTO @temp_semana
SELECT
	ROW_NUMBER() OVER(ORDER BY SEMANA DESC) N_SEMANA,
	SEMANA
FROM (
SELECT DISTINCT TOP 4 SEMANA
FROM [INFORMES3].[dbo].[INSTOCK_NUEVA_PLANILLA_RESUMEN_SEMANA]
ORDER BY SEMANA DESC
) TempSem


TRUNCATE TABLE [INFORMES3].[dbo].[NUEVO_INSTOCK_MAIL_CUADRO_RESUMEN]

INSERT INTO [INFORMES3].[dbo].[NUEVO_INSTOCK_MAIL_CUADRO_RESUMEN] (
	[CUADRO_RESUMEN]
	,[SEM4]
	,[SEM3]
	,[SEM2]
	,[SEM1]
	,[SEM_ACTUAL]
)

SELECT
	CUADRO_RESUMEN,
	SEM4,
	SEM3,
	SEM2,
	SEM1,
	SEM_ACTUAL
FROM (
	SELECT 
		SEMANA,
		(SUM(NUM_OH_VALORIZADO)/SUM(NUM_VTA_SEM_X_PERFIL)*100) INSTOCK,
		CUADRO_RESUMEN
	FROM (
		SELECT 
			CASE
				WHEN DATEPART(wk, GETDATE()) + DATEPART(yy, GETDATE())*100 = SEMANA THEN 'SEM_ACTUAL'
				ELSE 'SEM' + CONVERT(VARCHAR, SEMANA)
			END SEMANA,
			NUM_OH_VALORIZADO,
			NUM_VTA_SEM_X_PERFIL,
			CUADRO_RESUMEN,
			CATEGORIA
		FROM (

			/* Consulta instock semana Actual
			*/
			SELECT
				SEMANA,
				SUM([NUM_OH_VALORIZADO]) NUM_OH_VALORIZADO,
				ISNULL(SUM([NUM_VTA_SEM_X_PERFIL]), 0) NUM_VTA_SEM_X_PERFIL,
				CASE WHEN TOP_500 IS NOT NULL THEN 1 END TOP_500,
				CASE WHEN TOP_2100 IS NOT NULL THEN 1 END TOP_2100,
				CASE WHEN MMPP IS NOT NULL THEN 1 END MMPP,
				CASE
					WHEN (
						(DIVISION IN ('J01 - PGC COMESTIBLE')	AND SIS_REPOSICION IN ('Reposicion x ASR', 'Informar a ASR'))
						OR (DIVISION IN ('J02 - PGC NO COMESTIBLE') AND SIS_REPOSICION IN ('Reposicion x ASR'))
					) THEN 1
				END PGC,
				CASE
					WHEN (DIVISION in ('J05 - FLC', 'J06 - PANADERIA Y PASTELERIA','J07 - PLATOS PREPARADOS') AND SIS_REPOSICION in ('Reposicion x ASR'))
						THEN 1
				END PERECIBLES
			FROM [INFORMES3].[dbo].[INSTOCK_NUEVA_PLANILLA_HISTORIA] I
			LEFT JOIN [INFORMES3].[dbo].[TIENDAS_ASENTADAS] T
				ON I.COD_LOCAL = T.COD_LOCAL
			WHERE
				SEMANA = DATEPART(wk, GETDATE()) + DATEPART(yy, GETDATE())*100
				AND T.COD_LOCAL IS NULL
			GROUP BY
				SEMANA,
				TOP_500,
				TOP_2100,
				MMPP,
				DIVISION,
				SIS_REPOSICION

			UNION

			SELECT 
				TS.N_SEMANA AS SEMANA,
				SUM(NUM_OH_VALORIZADO) NUM_OH_VALORIZADO,
				SUM(NUM_VTA_SEM_X_PERFIL) NUM_VTA_SEM_X_PERFIL,
				CASE WHEN TOP_500 IS NOT NULL THEN 1 END TOP_500,
				CASE WHEN TOP_2100 IS NOT NULL THEN 1 END TOP_2100,
				CASE WHEN MMPP IS NOT NULL THEN 1 END MMPP,
				CASE
					WHEN (
						(DIVISION IN ('J01 - PGC COMESTIBLE')	AND SIS_REPOSICION IN ('Reposicion x ASR', 'Informar a ASR'))
						OR (DIVISION IN ('J02 - PGC NO COMESTIBLE') AND SIS_REPOSICION IN ('Reposicion x ASR'))
					) THEN 1
				END PGC,
				CASE
					WHEN (DIVISION in ('J05 - FLC', 'J06 - PANADERIA Y PASTELERIA','J07 - PLATOS PREPARADOS') AND SIS_REPOSICION in ('Reposicion x ASR'))
						THEN 1
				END PERECIBLES
			FROM [INFORMES3].[dbo].[INSTOCK_NUEVA_PLANILLA_RESUMEN_SEMANA] I
			LEFT JOIN [INFORMES3].[dbo].[TIENDAS_ASENTADAS] T
				ON I.COD_LOCAL = T.COD_LOCAL
			LEFT JOIN @temp_semana TS
				ON TS.SEMANA = I.SEMANA
			WHERE 
				I.SEMANA IN (SELECT SEMANA FROM @temp_semana)
				AND T.COD_LOCAL IS NULL
			GROUP BY
				TS.N_SEMANA,
				TOP_500,
				TOP_2100,
				MMPP,
				DIVISION,
				SIS_REPOSICION
		) I
		UNPIVOT (
			CATEGORIA FOR CUADRO_RESUMEN IN (
				TOP_500, TOP_2100, MMPP, PGC, PERECIBLES
			)
		) AS upvtable
	) I
	GROUP BY
		SEMANA,
		CUADRO_RESUMEN
) AS I
PIVOT (
	SUM(I.INSTOCK)
	FOR SEMANA IN (
		SEM4,
		SEM3,
		SEM2,
		SEM1,
		SEM_ACTUAL
	)
) AS pvtable

END