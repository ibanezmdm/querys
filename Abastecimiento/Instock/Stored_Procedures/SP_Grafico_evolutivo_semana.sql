USE [INFORMES3]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author: Sebastian Cornejo
-- Created at: 2019/11/12
-- Updated at: 2019/11/28
-- Description: Datos para Grafico Instock Evolutivo Semanal
-- ?? EXEC [INFORMES3].[dbo].[SP_GRAFICO_INSTOCK_SEMANA_v2]
-- =============================================
ALTER PROCEDURE [dbo].[SP_GRAFICO_INSTOCK_SEMANA_v2]

AS

BEGIN

	DECLARE @semanas INT
	SET @semanas = 4

	SELECT *
	FROM (

		SELECT 
			SEMANA,
			CUADRO_RESUMEN,
			SUM(NUM_OH_VALORIZADO)/SUM(NUM_VTA_SEM_X_PERFIL) * 100 INSTOCK
		FROM (

			SELECT
				SEMANA,
				NUM_OH_VALORIZADO,
				NUM_VTA_SEM_X_PERFIL,
				CUADRO_RESUMEN,
				FILTRO
			FROM (

				SELECT
					CASE WHEN TOP_500 IS NOT NULL THEN 1 END TOP_500,
					CASE WHEN TOP_2100 IS NOT NULL THEN 1 END TOP_2100,
					1 CIA,
					SEMANA,
					SUM([NUM_OH_VALORIZADO]) NUM_OH_VALORIZADO,
					SUM([NUM_VTA_SEM_X_PERFIL]) NUM_VTA_SEM_X_PERFIL
				FROM [INFORMES3].[dbo].[INSTOCK_NUEVA_PLANILLA_HISTORIA] I
				LEFT JOIN [INFORMES3].[dbo].[TIENDAS_ASENTADAS_HISTORIA] T
					ON I.FECHA_ACTUALIZ = T.FECHA
					AND I.COD_LOCAL = T.COD_LOCAL
				WHERE 
					SEMANA IN (DATEPART(WW, GETDATE())+DATEPART(YYYY, GETDATE())*100)
					AND T.COD_LOCAL IS NULL
				GROUP BY
					SEMANA,
					TOP_500,
					TOP_2100
			) AS I

			UNPIVOT (
				FILTRO FOR CUADRO_RESUMEN IN (
					TOP_500,
					TOP_2100,
					CIA
				)
			) AS upvtable
		) AS I

		GROUP BY
			SEMANA,
			CUADRO_RESUMEN


		UNION ALL


		SELECT
			I.SEMANA,
			CUADRO_RESUMEN,
			SUM(NUM_OH_VALORIZADO)/SUM(NUM_VTA_SEM_X_PERFIL) * 100 INSTOCK
		FROM (

			SELECT 
				SEMANA,
				NUM_OH_VALORIZADO,
				NUM_VTA_SEM_X_PERFIL,
				CUADRO_RESUMEN,
				FILTRO
			FROM (

				SELECT 
					CASE WHEN TOP_500 IS NOT NULL THEN 1 END TOP_500,
					CASE WHEN TOP_2100 IS NOT NULL THEN 1 END TOP_2100,
					1 CIA,
					I.SEMANA,
					SUM([NUM_OH_VALORIZADO]) NUM_OH_VALORIZADO,
					SUM([NUM_VTA_SEM_X_PERFIL]) NUM_VTA_SEM_X_PERFIL
				FROM [INFORMES3].[dbo].[INSTOCK_NUEVA_PLANILLA_RESUMEN_SEMANA] I
				LEFT JOIN (
					SELECT DISTINCT
						COD_LOCAL,
						DATEPART(WW, FECHA) + DATEPART(YY, FECHA)*100 SEMANA
					FROM [INFORMES3].[dbo].[TIENDAS_ASENTADAS_HISTORIA]
				) T
					ON I.SEMANA = T.SEMANA
					AND I.COD_LOCAL = T.COD_LOCAL
				WHERE
					I.SEMANA BETWEEN (DATEPART(WW, GETDATE())-@semanas+DATEPART(YYYY, GETDATE())*100) AND (DATEPART(WW, GETDATE())-1+DATEPART(YYYY, GETDATE())*100)
					AND T.COD_LOCAL IS NULL
				GROUP BY
					I.SEMANA,
					TOP_500,
					TOP_2100
			
			) AS I
			UNPIVOT (
				FILTRO FOR CUADRO_RESUMEN IN (
					CIA,
					TOP_500,
					TOP_2100
				)
			) upvtable
		
		) AS I
		GROUP BY
			I.SEMANA,
			CUADRO_RESUMEN

	) AS T
	PIVOT (
		SUM(T.INSTOCK)
		FOR T.CUADRO_RESUMEN IN (CIA, TOP_500, TOP_2100)
	) AS pivotTable

END