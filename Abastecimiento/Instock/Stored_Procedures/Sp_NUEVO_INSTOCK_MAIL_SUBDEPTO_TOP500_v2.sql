USE [INFORMES3]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author: <Sebastian Cornejo>
-- Created_at: <2020/01/07>
-- Updated_at: <yyyy/mm/dd>
-- EXEC [INFORMES3].[dbo].[Sp_NUEVO_INSTOCK_MAIL_SUBDEPTO_TOP500_v2]
-- =============================================
ALTER PROCEDURE [dbo].[Sp_NUEVO_INSTOCK_MAIL_SUBDEPTO_TOP500_v2]
AS
BEGIN

	DECLARE @temp_semana AS TABLE (
		N_SEMANA INT,
		SEMANA INT
	)

	INSERT INTO @temp_semana
	SELECT
		ROW_NUMBER() OVER(ORDER BY SEMANA DESC) N_SEMANA,
		SEMANA
	FROM (
		SELECT DISTINCT TOP 4 SEMANA
		FROM [INFORMES3].[dbo].[INSTOCK_NUEVA_PLANILLA_RESUMEN_SEMANA]
		ORDER BY SEMANA DESC
	) TempSem

	TRUNCATE TABLE [INFORMES3].[dbo].[NUEVO_INSTOCK_MAIL_SUBDEPTO_TOP500]

	INSERT INTO [INFORMES3].[dbo].[NUEVO_INSTOCK_MAIL_SUBDEPTO_TOP500] (
		[SUBDEP]
		,[SEM4]
		,[SEM3]
		,[SEM2]
		,[SEM1]
		,[SEM_ACTUAL]
	)


	SELECT
		SUBDEP,
		ISNULL(SEM4, 0) SEM4,
		ISNULL(SEM3, 0) SEM3,
		ISNULL(SEM2, 0) SEM2,
		ISNULL(SEM1, 0) SEM1,
		ISNULL(SEM_ACTUAL, 0) SEM_ACTUAL
	FROM (

		/* Consulta instock semana Actual
		*/
		SELECT
			'SEM_ACTUAL' SEMANA,
			SUBDEP,
			CONVERT(NUMERIC(12,2) , ISNULL(SUM([NUM_OH_VALORIZADO]) / SUM([NUM_VTA_SEM_X_PERFIL]) * 100, 0)) AS INSTOCK
		FROM [INFORMES3].[dbo].[INSTOCK_NUEVA_PLANILLA_HISTORIA] I
		WHERE
			SEMANA = DATEPART(YYYY, GETDATE() + 7 - DATEPART(DW, GETDATE())) * 100 + DATEPART(WEEK, GETDATE() + 7 - DATEPART(DW, GETDATE()))
			AND COD_LOCAL NOT IN (SELECT * FROM INFORMES3.dbo.TIENDAS_ASENTADAS)
			AND (
				(DIVISION in ('J05 - FLC', 'J01 - PGC COMESTIBLE','J02 - PGC NO COMESTIBLE') AND SIS_REPOSICION IN ('Reposicion x ASR', 'Informar a ASR'))
				OR (DIVISION in ('J06 - PANADERIA Y PASTELERIA','J07 - PLATOS PREPARADOS') AND SIS_REPOSICION IN ('Reposicion x ASR'))
			)
			AND TOP_500 = 'TOP500'
		GROUP BY
			SEMANA,
			SUBDEP

		UNION

		SELECT 
			'SEM' + CONVERT(VARCHAR(2), TS.N_SEMANA) AS SEMANA,
			SUBDEP,
			CONVERT(NUMERIC(12,2), ISNULL(SUM([NUM_OH_VALORIZADO]) / SUM([NUM_VTA_SEM_X_PERFIL]) * 100, 0)) AS INSTOCK
		FROM [INFORMES3].[dbo].[INSTOCK_NUEVA_PLANILLA_RESUMEN_SEMANA] I
		LEFT JOIN @temp_semana TS
			ON TS.SEMANA = I.SEMANA
		WHERE 
			I.SEMANA IN (SELECT SEMANA FROM @temp_semana)
			AND COD_LOCAL NOT IN (SELECT * FROM INFORMES3.dbo.TIENDAS_ASENTADAS)
			AND (
				(DIVISION in ('J05 - FLC', 'J01 - PGC COMESTIBLE','J02 - PGC NO COMESTIBLE') AND SIS_REPOSICION IN ('Reposicion x ASR', 'Informar a ASR'))
				OR (DIVISION in ('J06 - PANADERIA Y PASTELERIA','J07 - PLATOS PREPARADOS') AND SIS_REPOSICION IN ('Reposicion x ASR'))
			)
			AND TOP_500 = 'TOP500'
		GROUP BY
			TS.N_SEMANA,
			SUBDEP
	) AS I
	PIVOT (
		SUM(I.INSTOCK)
		FOR SEMANA IN (
			SEM4,
			SEM3,
			SEM2,
			SEM1,
			SEM_ACTUAL
		)
	) AS pvtable

END 
